public import define.context

import arch.arch
import arch.riscv.csr
import lib.io
import trap.syscall
import lib.except
import proc.proc

// trap handler in 'traphand.S'
extern declare _handleTrap: ()

// tick of timer
var tick = 0 as USize

// handle timer interrupt
def handleTimer() {
  // TODO: remove this log
  tick += 1 as USize
  if tick % 100 as USize == 0 as USize {
    io <<< "100 ticks!\n"
  }
  // tell processor a tick passed
  tick()
  // acknowledge soft interrupt
  setSip(getSip() & ~SIE_SSIE)
}

// handle system call
def handleSyscall(tf: TrapFrame var&) {
  tf.sepc += 4 as USize
  tf.x[10] = runSyscall(tf) as USize
}

// handle page fault
def handlePageFault(tf: TrapFrame var&) {
  io <<< "scause  = " <<< tf.scause <<< '\n'
  io <<< "stval   = 0x" <<$ tf.stval <<< '\n'
  io <<< "sepc    = 0x" <<$ tf.sepc <<< '\n'
  panic("page fault!")
}

// initialize trap handling
public def initTrap() {
  setSscratch(0 as USize)
  setStvec(_handleTrap as USize)
}

// trap handler
extern def handleTrap(tf: TrapFrame var&) {
  when tf.scause {
    TRAP_S_SOFT_INT {
      handleTimer()
    }
    TRAP_U_SYSCALL {
      handleSyscall(tf)
    }
    TRAP_INST_FAULT, TRAP_LOAD_FAULT, TRAP_STORE_FAULT {
      handlePageFault(tf)
    }
    else {
      io <<< "sstatus = 0x" <<$ tf.sstatus <<< '\n'
      io <<< "sepc    = 0x" <<$ tf.sepc <<< '\n'
      io <<< "scause  = " <<< tf.scause <<< '\n'
      io <<< "stval   = 0x" <<$ tf.stval <<< '\n'
      panic("uexpected trap!")
    }
  }
}
