public import sync.spinlock
public import lib.queue

public struct Semaphore {
  lock: Spinlock,
  value: i32,
  wait_queue: Queue,
}

import lib.except
import proc.proc

public def newSemaphore(name: u8*, value: i32): Semaphore {
  var lock: Spinlock
  lock.init(name)
  [Semaphore] {lock, value, newQueue()}
}

public def newSemaphore(name: u8*): Semaphore {
  newSemaphore(name, 1)
}

public def del(this: Semaphore var&) {
  this.wait_queue.del()
}

public def wait(this: Semaphore var&) {
  this.lock.acquire()
  if this.value > 0 {
    this.value -= 1
    this.lock.release()
  }
  else {
    this.wait_queue.push(getCurrentTid() as USize)
    this.lock.release()
    yield()
  }
}

public def signal(this: Semaphore var&) {
  this.lock.acquire()
  if this.wait_queue.empty() {
    this.value += 1
    this.lock.release()
  }
  else {
    let tid = this.wait_queue.pop()
    this.lock.release()
    wakeUp(tid as Tid)
  }
}
