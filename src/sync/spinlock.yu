import sync.intr
import lib.except

// structure definition of spinlock
public struct Spinlock {
  // is the lock held
  locked: u32,
  // name of lock
  name: u8*,
}

// implementation of spinlock, in 'slimpl.c'
extern declare __acquire: (Spinlock var&)
extern declare __release: (Spinlock var&)

public def init(lock: Spinlock var&, name :u8*) {
  lock.locked = 0 as u32
  lock.name = name
}

public def isHold(lock: Spinlock&): bool {
  pushOff()
  let r = lock.locked != 0 as u32
  popOff()
  r
}

public def acquire(lock: Spinlock var&) {
  // disable interrupts to avoid deadlock
  pushOff()
  assert(!lock.isHold(), "acquire")
  // perform acquire
  __acquire(lock)
}

public def release(lock: Spinlock var&) {
  assert(lock.isHold(), "release")
  // perform release
  __release(lock)
  popOff()
}
